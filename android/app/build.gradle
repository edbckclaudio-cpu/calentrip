apply plugin: 'com.android.application'

android {
    namespace "digital.calentrip.android"
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "digital.calentrip.android"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 148
        versionName "1.3.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }

    signingConfigs {
        release {
            def envStore = System.getenv("UPLOAD_STORE_FILE")
            if (envStore) {
                storeFile file(envStore)
                storePassword System.getenv("UPLOAD_STORE_PASSWORD") ?: ""
                keyAlias System.getenv("UPLOAD_KEY_ALIAS") ?: ""
                keyPassword System.getenv("UPLOAD_KEY_PASSWORD") ?: ""
            } else {
                try {
                    def propsFile = rootProject.file("local.properties")
                    if (propsFile.exists()) {
                        def Properties p = new Properties()
                        p.load(new FileInputStream(propsFile))
                        def lpStore = p.getProperty("UPLOAD_STORE_FILE")
                        def lpStorePassword = p.getProperty("UPLOAD_STORE_PASSWORD")
                        def lpKeyAlias = p.getProperty("UPLOAD_KEY_ALIAS")
                        def lpKeyPassword = p.getProperty("UPLOAD_KEY_PASSWORD")
                        if (lpStore) {
                            storeFile file(lpStore)
                            storePassword lpStorePassword ?: ""
                            keyAlias lpKeyAlias ?: ""
                            keyPassword lpKeyPassword ?: ""
                        }
                    }
                } catch (Exception e) {
                    logger.info("local.properties not loaded for release signing")
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            } else if (System.getenv("UPLOAD_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}


repositories {
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    // Cordova plugin project is optional; include only if present
    if (project.findProject(':capacitor-cordova-android-plugins') != null) {
        implementation project(':capacitor-cordova-android-plugins')
    }
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
